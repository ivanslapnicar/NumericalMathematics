### A Pluto.jl notebook ###
# v0.14.7

using Markdown
using InteractiveUtils

# ‚ïî‚ïê‚ï° d358978b-fa6a-4ac5-afdc-242d5556cc82
begin
	import Pkg
	Pkg.activate(mktempdir())
	Pkg.add("PlutoUI")
	Pkg.add("Polynomials")
end

# ‚ïî‚ïê‚ï° de15c2d3-d314-47cc-9bd1-9c7f3109b92e
using PlutoUI, Polynomials

# ‚ïî‚ïê‚ï° 7b2b10d7-46d6-406b-ab2b-951fbaedf8e9
TableOfContents(title="üìö Table of Contents", aside=true)

# ‚ïî‚ïê‚ï° a481f540-027d-11eb-10b5-5bd4e827e274
md"""
# Introduction

__Numerical analysis is the science of computing the solutions of problems that are posed
mathematically in the field of real or complex numbers.__

Let us just give a couple of examples.
"""

# ‚ïî‚ïê‚ï° c2a8c4e0-027d-11eb-26fe-1f7bf12091f4
md"""
## Computation of $\pi \approx 3.14159265358979$

 $\pi$ is known as the ratio of the circumference of a circle and its diameter.
"""

# ‚ïî‚ïê‚ï° eab6ace0-027d-11eb-0f82-db588f0a253b
md"""
### Archimedes Method

Inscribe a regular $n$-gon in a circle of radius $1$. Compute the perimeter of its
upper half. This is easiest to do if $n=2^k$. Using geometric reasoning,

$$
p(n) = 2n \sin \: \frac{\pi}{2n}.$$

Even though this refers to $\pi$ we can compute this value for $n=1,2,\cdots, $ without
computing $\pi$ and without punching the $\sin$ button on our calculator. (Computing
the $\sin$ function requires you to know $\pi$!)
"""

# ‚ïî‚ïê‚ï° 3d725c70-027b-11eb-10cb-5b6e56311aa1
md"""
From geometric reasoning, we know that

$$
\sin \: \frac{\pi}{2} =1, \quad \sin \: \frac{\pi}{4} = \frac{\sqrt{2}}{2}.$$

So

$$
p(1)= 2 \cdot \sin \: \frac{\pi}{2} =2, \quad p(2) = 4 \cdot \sin \: \frac{\pi}{4} = 2\sqrt{2}= 2.828427125$$

but what is $p(4) = 8 \cdot \sin \: \displaystyle\frac{\pi}{8}$. Use half angle formulas!

$$
\sin \: \frac{\theta}{2} = \sqrt{\frac{1-\cos \: \theta }{2}}, \quad \cos \: \theta = \sqrt{1-\sin^2 \: \theta }.$$

That yields

$$
\sin \: \frac{\pi}{8} = 0.382683432.$$

Thus

$$
p(4) = 8 \cdot \sin \: \frac{\pi}{8}=3.061467459.$$

Continuing this process we get

n | $\sin\displaystyle\frac{\pi}{2n}$ | $p(n)$
:---|:---|:---
1 | 1 | 2
2 | $\sqrt{0.5}$ | 2.82842712
4 |  0.382683432 | 3.061467459
8 | 0.195090322 | 3.121445152
16 | 0.09801714 | 3.136548491
32 | 0.049067674 | 3.140331157

This method is slow, but "sure". 
"""

# ‚ïî‚ïê‚ï° 3dea5041-85b9-4d8b-9dcf-2d39c8481314
steps=10

# ‚ïî‚ïê‚ï° a47b53a0-027f-11eb-256d-f7d5ce0f97e3
let
	c=0.0
	for n=1:steps
    	s=sqrt((1-c)/2)
    	c=sqrt(1-s^2)
    	println("2n = ", 2^(n+1), ", approximation of œÄ = ",2^(n+1)*s)
	end
end

# ‚ïî‚ïê‚ï° 4d68f850-027b-11eb-14a1-1741ea4ce7c2
# Accurate œÄ
œÄ

# ‚ïî‚ïê‚ï° 6ac49527-05f0-4a0a-ad99-aba5d4b07c17
md"""
__Problem.__ Increase the number of steps and explain what happens.
"""

# ‚ïî‚ïê‚ï° 8443676d-70db-4f63-8b8a-a7e96175f813
md"""
Later, we will give a modern enhancement that
is faster. Letting $h=1/(2n)$ we have that

$$
p(n) = \frac{\sin \: \pi h}{h} = \pi -a_2 h^2 + a_4 h^4 - \cdots$$

where $a_k = \pi^{k+1}/(k+1)!$. Thus this converges to $\pi$ at roughly the rate of
$O(h^2)$. This is an __approximation problem__. There is no finite algorithm
to compute $\pi$, since it is a transcendental number (irrational and not the root of
any polynomial with integer coefficients). However, we can approximate it arbitrarily
well.
"""

# ‚ïî‚ïê‚ï° bd7a0ad8-8b31-42cf-94e9-881711a3c40e
md"""
## Quadratic equation

The following problem poses completely different issues.

Let us compute roots of $p(x) = ax^2 + bx +c =0$ for constants $a$, $b$ and $c$.

The solution is just

$$
x_{1,2} = \frac{ -b \pm \sqrt{b^2 - 4ac}}{2a}. \tag{1}$$

How do we go about writing a code to compute this.

Take care of special cases.

__Case I.__ $a=0, b \neq 0$.

It is no longer a quadratic, it is linear. Only solution is

$$
x_1 = -c/b.$$

__Case II.__ $a=b=0$

If $c \neq 0$, no solution. If $c=0$, all $x$ are solutions.

The cases you spent time on in high school had to do with the discriminant
$b^2 - 4ac$.

__Case III.__ $b^2 -4ac < 0$. Two Complex Roots (not real).

$$
x_{1,2} = -\frac{b}{2a} \pm \mathbf{i} \frac{\sqrt{4ac-b^2}}{2a}, \quad \mathbf{i}^2 = -1.$$

__Case IV.__ $b^2 - 4ac =0$. One Double Root (real)

$$
x_1 = x_2 = -\frac{b}{2a}.$$

__Case V.__ $b^2 -4ac > 0$. Two Distinct Real Roots.
Use Formula (1)?
"""

# ‚ïî‚ïê‚ï° b5fe78a7-5258-41ba-9544-c0cd64496ef5
begin
	a=1
	b=2
	c=10.0^(-17)
	x‚ÇÅ=(-b-sqrt(b*b-4*a*c))/(2*a)
	x‚ÇÇ=(-b+sqrt(b*b-4*a*c))/(2*a)
	
	x‚ÇÅ,x‚ÇÇ
end

# ‚ïî‚ïê‚ï° bca1eb35-dd3d-41af-8130-a8035551ec49
md"""
The two real roots are (to about 17 digits)

$$
x_1 = -2, \quad x_2 = -5 \cdot 10^{-18}.$$

The above algorithm gets $x_1$ right, but $x_2 = 0$. The standard double precision floating-point number format, `Float64`, stores about $15$ decimal digits (54 binary digits) and in those 15 digits $\sqrt{b^2 -4ac}-b =0$. 
That is because we are subtracting two close numbers and one of these is approximate, so this difference is "all rounding error". A simple observation gets around this problem.

The "large" root of the quadratic in __Case V__ is

$$
x_1 = \frac{ -b - \mathrm{sign}(b) \sqrt{b^2 - 4ac}}{2a}$$

and the two roots satisfy

$$
x_1 x_2 = \frac{c}{a}.$$

Notice that except inside the square root, we are adding numbers of the same sign!
After some algebra, we get a formula for the small root

$$
x_2 = \frac{c}{a x_1} = \frac{-2c}{ b + \mathrm{sign}(b) \sqrt{b^2 - 4ac}}.$$

Using this formula we compute both roots to near machine precision.

In this example, we have an exact formula, but in floating point arithmetic the standard quadratic formula
yields results that are significantly different from what it yields in real arithmetic.
"""

# ‚ïî‚ïê‚ï° 2f084ae2-1e38-4c0b-83b6-da9f025565b1
begin
	x‚ÇÉ=(-b-sign(b)*sqrt(b*b-4*a*c))/(2*a)
	x‚ÇÑ=-2*c/(b+sign(b)*sqrt(b*b-4*a*c))
	x‚ÇÉ,x‚ÇÑ
end

# ‚ïî‚ïê‚ï° f8cf034f-b149-4ff9-8a7e-bc8143581d51
md"""
The following function implements all five cases. Try out the function with different inputs and cover all five cases.
"""

# ‚ïî‚ïê‚ï° 0738969f-7c4f-4f8e-92de-62cb0cd6510c
function quadroots(a,b,c)
    # Function to find the roots of the quadratic equation
    # given coefficients a,b, and c.
    # This function takes no account of scaling. 
    if a==0
        #  Check odd cases when a=0 
        if b==0
            if c==0
                return "all numbers are roots a=b=c=0"
            else
                return "no roots a=b=0, c ne 0"
            end
        else
            x‚ÇÅ=-c/b
            x‚ÇÇ=x‚ÇÅ
            ier="one root a=0"
        end
    else
        Œî = b*b-4*a*c
        if Œî < 0
            # Two complex roots computed with real arithmetic
            ximaginary=sqrt(-Œî)/(2*a)
            xreal=-b/(2*a)
            x‚ÇÅ=xreal+im*ximaginary
            # x‚ÇÇ is the complex conjugate of x‚ÇÅ, 
            # x‚ÇÇ = xreal - im*ximaginary
            x‚ÇÇ=conj(x‚ÇÅ)
        else
            if b==0
                # Since Julia handles complex arithmetic without a
                # blink, we can just use the formula in this case.
                x‚ÇÅ=sqrt(-c)/a
                x‚ÇÇ=-x‚ÇÅ
            else
                # Case where there are two real roots.
                x‚ÇÅ=(-b-sign(b)*sqrt(Œî))/(2*a)
                x‚ÇÇ=-2*c/(b+sign(b)*sqrt(Œî))
            end
        end
        ier="roots are good"
    end
    x‚ÇÅ, x‚ÇÇ, ier
end

# ‚ïî‚ïê‚ï° cd29c4bc-9cfa-40bc-bc80-74ee5a635a94
quadroots(1,0,7)

# ‚ïî‚ïê‚ï° af57717b-4dca-4b54-8272-e98fe76fd845
quadroots(a,b,c)

# ‚ïî‚ïê‚ï° 8c4dfbc0-0ee6-11eb-2055-a7fdabff7984
md"""
## Julia is fast and open

`Julia` is __fast__ and is the first language to solve the "two language problem", for example using Matlab or Python for development and C, C++ or C# for speed. Every function is compiled after the first call using [JIT compiler](https://en.wikipedia.org/wiki/Just-in-time_compilation), in this case [LLVM](https://en.wikipedia.org/wiki/LLVM). 

`Julia` is  __open__ since
* The complete source code is always accessible on GitHub.
* MIT license.
* Macro `@which` makes orientation easy.
* LLVM and assembler code are easily displayed.
"""

# ‚ïî‚ïê‚ï° b57ce420-0ee6-11eb-3909-9342f41a5255
# Contents of package Polynomials
varinfo(Polynomials)

# ‚ïî‚ïê‚ï° c0a55620-0ee6-11eb-2297-d56f329d7bd1
# ?Polynomial

# ‚ïî‚ïê‚ï° d3cbd530-0ee6-11eb-1a80-4ded54e128cd
p=Polynomial([c,b,a])

# ‚ïî‚ïê‚ï° e3f0fb20-0ee6-11eb-0c86-cfbcb66da20f
roots(p)

# ‚ïî‚ïê‚ï° ee73691e-0ee6-11eb-2953-23bd44f54b81
@which roots(p)

# ‚ïî‚ïê‚ï° f9f65730-0ee6-11eb-0650-0b7c48826738
md"
You can also take a look at the corresponding GitHub file for the
most recent version.
"

# ‚ïî‚ïê‚ï° 0c3d24a0-0ee7-11eb-1781-4b863e8378de
# Output is in Julia terminal
@code_llvm quadroots(1,0,7)

# ‚ïî‚ïê‚ï° 1fe64130-0ee7-11eb-3db0-bbd29c571cda
# Output is in Julia terminal
@code_native quadroots(1,0,7)

# ‚ïî‚ïê‚ï° Cell order:
# ‚ï†‚ïêd358978b-fa6a-4ac5-afdc-242d5556cc82
# ‚ï†‚ïêde15c2d3-d314-47cc-9bd1-9c7f3109b92e
# ‚ï†‚ïê7b2b10d7-46d6-406b-ab2b-951fbaedf8e9
# ‚ïü‚îÄa481f540-027d-11eb-10b5-5bd4e827e274
# ‚ïü‚îÄc2a8c4e0-027d-11eb-26fe-1f7bf12091f4
# ‚ïü‚îÄeab6ace0-027d-11eb-0f82-db588f0a253b
# ‚ïü‚îÄ3d725c70-027b-11eb-10cb-5b6e56311aa1
# ‚ï†‚ïê3dea5041-85b9-4d8b-9dcf-2d39c8481314
# ‚ï†‚ïêa47b53a0-027f-11eb-256d-f7d5ce0f97e3
# ‚ï†‚ïê4d68f850-027b-11eb-14a1-1741ea4ce7c2
# ‚ïü‚îÄ6ac49527-05f0-4a0a-ad99-aba5d4b07c17
# ‚ïü‚îÄ8443676d-70db-4f63-8b8a-a7e96175f813
# ‚ïü‚îÄbd7a0ad8-8b31-42cf-94e9-881711a3c40e
# ‚ï†‚ïêb5fe78a7-5258-41ba-9544-c0cd64496ef5
# ‚ïü‚îÄbca1eb35-dd3d-41af-8130-a8035551ec49
# ‚ï†‚ïê2f084ae2-1e38-4c0b-83b6-da9f025565b1
# ‚ïü‚îÄf8cf034f-b149-4ff9-8a7e-bc8143581d51
# ‚ï†‚ïê0738969f-7c4f-4f8e-92de-62cb0cd6510c
# ‚ï†‚ïêcd29c4bc-9cfa-40bc-bc80-74ee5a635a94
# ‚ï†‚ïêaf57717b-4dca-4b54-8272-e98fe76fd845
# ‚ïü‚îÄ8c4dfbc0-0ee6-11eb-2055-a7fdabff7984
# ‚ï†‚ïêb57ce420-0ee6-11eb-3909-9342f41a5255
# ‚ï†‚ïêc0a55620-0ee6-11eb-2297-d56f329d7bd1
# ‚ï†‚ïêd3cbd530-0ee6-11eb-1a80-4ded54e128cd
# ‚ï†‚ïêe3f0fb20-0ee6-11eb-0c86-cfbcb66da20f
# ‚ï†‚ïêee73691e-0ee6-11eb-2953-23bd44f54b81
# ‚ïü‚îÄf9f65730-0ee6-11eb-0650-0b7c48826738
# ‚ï†‚ïê0c3d24a0-0ee7-11eb-1781-4b863e8378de
# ‚ï†‚ïê1fe64130-0ee7-11eb-3db0-bbd29c571cda
