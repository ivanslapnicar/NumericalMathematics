### A Pluto.jl notebook ###
# v0.14.7

using Markdown
using InteractiveUtils

# ‚ïî‚ïê‚ï° d0d29fe8-6542-4968-a695-03371ba85543
begin
	import Pkg
	Pkg.activate(mktempdir())
    Pkg.add([
        Pkg.PackageSpec(name="PlutoUI"),
		Pkg.PackageSpec(name="Plots"),
		Pkg.PackageSpec(name="ForwardDiff")
    ])
end

# ‚ïî‚ïê‚ï° 3a20c826-03cd-4c06-a0c6-7dc657067feb
begin
	using PlutoUI, Plots, ForwardDiff
	plotly()
end

# ‚ïî‚ïê‚ï° 7bab2ccd-e6d7-479c-abef-d9ed8eefaf2a
TableOfContents(title="üìö Table of Contents", aside=true)

# ‚ïî‚ïê‚ï° 1198a836-bb14-4b8e-9f30-160097bc4507
md"""
# Nonlinear Equations

__Problem.__ Find zeros of the function $f(x)$ in the closed interval $[a,b]$, that is, solve the equation

$$
f(x)=0, \quad x\in[a,b]. \qquad\qquad (1)$$

It holds:

If $f$ is __continuous__ on $[a,b]$ and if $f(a)\cdot f(b)<0$, then there exists at least one point $\xi\in(a,b)$ such

$$
f(\xi)=0.$$

If, additionally, $f'(x)\neq 0$ za $x\in(a,b)$, then $\xi$ is __unique__.

Thus, the equation (1) can be solved in two steps:

1. Find the interval $[a,b]$ in which function $f$ has unique zero $\xi$,
2. Approximate $\xi$ with accuracy given in advance.

We shall desribe four methods:

1. bisection,
2. simple iterations,
3. Newton's method (Tangent method), and 
4. Secant method.

Given starting approximation $x_0$, all methods   generate sequence of points $x_n$ which, under certain conditions, converges towards the solution
$\xi$. 

Method has __order of convergence__ equal to $r>0$ if there exists $A>0$ such that

$$
|\xi-x_{n+1}|\leq A|\xi-x_n|^r.$$

__Remark.__ Proofs of statements in this notebook and examples can be found in many textbooks.

"""

# ‚ïî‚ïê‚ï° 7942e1ac-57f4-493c-9d09-de4154dcd7b9
md"""
# Bisection

Starting with the interval $[a,b]\equiv [a_0,b_0]$, construct a sequence of intervals  

$$
[a_0,b_0]\supset [a_1,b_1]\supset [a_2,b_2]\supset [a_3,b_3] \supset \cdots,$$

where $f(a_n)\cdot f(b_n)\leq 0$, and the sequence of points

$$
x_{n+1}=\frac{a_n+b_n}{2}.$$

__Speed of convergence__ is __linear__ since 

$$
|\xi-x_{n+1}|\leq \frac{1}{2}|\xi-x_n|,$$

and the __approximation error__ is bounded by

$$
|\xi-x_{n+1}|\leq \frac{1}{2}|a_n-b_n|.$$
"""

# ‚ïî‚ïê‚ï° 9d707705-29a2-47fc-87b8-d39ff967efb5
function Bisection(f::Function,a::Number,b::Number,œµ::Float64=1e-10)
    fa=f(a)
    fb=f(b)
    T=Float64
    x=T
    fx=T
    if fa*fb>zero(T)
        return "Wrong interval"
    end
    iter=0
    while b-a>œµ && iter<1000
        x=(b+a)/2.0
        fx=f(x)
        if fa*fx<zero(T)
            b=x
            fb=fx
        else
            a=x
            fa=fx
        end
        iter+=1
        # @show x,fx
    end
    return x,fx,iter
end

# ‚ïî‚ïê‚ï° 0233a80f-dfc6-420c-ae39-60828b19010a
md"""
## Examples

Given are functions and intervals:

$$
\begin{aligned}
f_1(x)&=e^x-x- \frac{5}{4},\quad &x\in [-2,2],\\
f_2(x)&=e^{-2x}\sin (6x)-\frac{2}{3}\,x-\frac{1}{2},\quad &x\in[-1,2],\\
f_3(x)&=x^3-6x+2,\quad &x\in[-4,4],\\
f_4(x)&=0.001\,x+0.5+\frac{\pi}{2}+\arctan(x),\quad &x\in[-1000,1000],\\
f_5(x)&=1000\,(x-4)-e^x,\quad &x\in[-10,10].
\end{aligned}$$
"""

# ‚ïî‚ïê‚ï° 0be4ad52-497f-48a4-9f53-b6d1291beb58
begin
	f‚ÇÅ(x)=exp(x)-x-5.0/4
	(a‚ÇÅ,b‚ÇÅ)=(-1,1)
	f‚ÇÇ(x)=exp(-2x)*sin(6x)+2x/3-1.0/2
	(a‚ÇÇ,b‚ÇÇ)=(-1,2)
	f‚ÇÉ(x)=x^3-6*x+2
	(a‚ÇÉ,b‚ÇÉ)=(-4,4)
	f‚ÇÑ(x)=0.001x+0.5+œÄ/2+atan(x)
	(a‚ÇÑ,b‚ÇÑ)=(-1000,1000)
	f‚ÇÖ(x)=1000(x-4)-exp(x)
	(a‚ÇÖ,b‚ÇÖ)=(-10,10)
end

# ‚ïî‚ïê‚ï° 5a17ece9-a499-42c7-8dc8-cc9035f1b311
md"""
Using function graph we determine intervals which contain zeros, which we then compute and plot. 
"""

# ‚ïî‚ïê‚ï° 910d7790-488d-48b8-a951-eee68774f79f
function Zeros(f,a,b,Intervals)
    plot(f,a,b,label="f(x)")
    for i=1:length(Intervals)
        iab=Intervals[i]
        x,y,iter=Bisection(f,iab[1],iab[2])
        scatter!([x],[y],label="Zero")
    end
    scatter!()
end

# ‚ïî‚ïê‚ï° 5d233a49-774b-479c-8c80-5de46817614e
# Function f‚ÇÅ(x)
plot(f‚ÇÅ,a‚ÇÅ,b‚ÇÅ,label="f(x)")

# ‚ïî‚ïê‚ï° aedcd7b9-8ef5-48a8-8ef8-d7dab4b733e0
Intervals‚ÇÅ=((-1,0),(0,1))

# ‚ïî‚ïê‚ï° e7e92425-287a-482b-bb1e-8168b2b130f1
Zeros(f‚ÇÅ,a‚ÇÅ,b‚ÇÅ,Intervals‚ÇÅ)

# ‚ïî‚ïê‚ï° 56a6bc1f-7ff8-464d-a864-ec7a19e3f9c3
# Function f‚ÇÇ(x)
plot(f‚ÇÇ,a‚ÇÇ,b‚ÇÇ,label="f(x)")

# ‚ïî‚ïê‚ï° 7097ec9a-b556-4be7-a356-9c9d5003dd73
Intervals‚ÇÇ=((-1,-0.4),(-0.4,0.2),(0.2,0.6),(0.6,1))

# ‚ïî‚ïê‚ï° bdb9d5d9-861c-4fd1-89d5-e0622120f944
Zeros(f‚ÇÇ,a‚ÇÇ,b‚ÇÇ,Intervals‚ÇÇ)

# ‚ïî‚ïê‚ï° f7802590-744f-4175-ba60-5d801fa252ec
# Computation of an individual zero
Bisection(f‚ÇÇ,0.2,0.6)

# ‚ïî‚ïê‚ï° d193fd54-c5a7-45f4-a61d-ae532bae4f44
# Function f‚ÇÉ(x)
plot(f‚ÇÉ,a‚ÇÉ,b‚ÇÉ,label="f(x)")

# ‚ïî‚ïê‚ï° 52fbcdb5-ade7-48c0-bc09-0154f3d615b8
begin
	Intervals‚ÇÉ=((-4,-2),(0,1),(2,3))
	Zeros(f‚ÇÉ,a‚ÇÉ,b‚ÇÉ,Intervals‚ÇÉ)
end

# ‚ïî‚ïê‚ï° e1f25e30-1ff7-4a91-b7cf-393c36c438b6
begin
	# Function f‚ÇÑ(x)
	Intervals‚ÇÑ=[(-600,-400)]
	Zeros(f‚ÇÑ,a‚ÇÑ,b‚ÇÑ,Intervals‚ÇÑ)
end

# ‚ïî‚ïê‚ï° f33b37c9-2f88-422b-ac9f-40d7945ccb47
begin
	# Function f‚ÇÖ(x)
	Intervals‚ÇÖ=((0,5),(5,10))
	Zeros(f‚ÇÖ,a‚ÇÖ,b‚ÇÖ,Intervals‚ÇÖ)
end

# ‚ïî‚ïê‚ï° 4c7dc70c-1832-461c-ad48-5e358b49661f
plot!(legend=:bottomright)

# ‚ïî‚ïê‚ï° 69c1d80b-3d24-4ded-a226-391b337805e5
md"""
# Simple iterations

We are solving equation of the form 

$$
x=\varphi(x).  \qquad\qquad (2)$$

__Fixed Point Theorem. (Banach)__ Let

$$\varphi:[a,b]\to \mathbb{R}$$

be a __continuously differentiable function__ and let

$$\begin{aligned}
\varphi(x) &\in [a,b] \quad  \forall x\in [a,b], \\
|\varphi'(x)|&\leq q<1 \quad \forall x\in(a,b).
\end{aligned}\qquad\qquad (3)$$

Then there exists unique __fixed point__ $\xi \in [a,b]$ for which
$\xi=\varphi(\xi)$. 

Furthermore, for arbitrary starting point  $x_0\in[a,b]$, the sequence

$$
x_n=\varphi(x_{n-1}),\quad n=1,2,3,\ldots,$$

converges to $\xi$. The following __error bounds__ hold:

$$\begin{aligned}
|\xi-x_n|&\leq \displaystyle\frac{q^n}{1-q}|x_1-x_0|, \\
|\xi-x_n|&\leq \displaystyle\frac{q}{1-q}|x_n-x_{n-1}|, \\
|\xi-x_n|&\leq q|\xi-x_{n-1}|.
\end{aligned}$$

Thus, the convergence is __linear__.

__Problem.__ Find and study the proof of the theorem.
"""

# ‚ïî‚ïê‚ï° d794399b-9c03-47a1-8a37-40f8545a0d46
function Iteration(œÜ::Function,x::Number,œµ::Float64=1e-10)
    Œæ=œÜ(x)
    iter=0
    while abs(x-Œæ)>œµ && iter<1000
        x=Œæ
        Œæ=œÜ(x)
        iter+=1
    end
    Œæ,iter
end

# ‚ïî‚ïê‚ï° 5cc494f2-a789-4455-9ac9-925428d274fe
md"""
In order to use simple iteration, we need to transform form (1) into form (2) such that the condition (3) holds.

To estimate derivative we can use the package `Calculus.jl` which approximates derivatives using finite differences, or the package
[`ForwardDiff.jl`](https://github.com/JuliaDiff/ForwardDiff.jl) which uses [automatic differentiation](https://en.wikipedia.org/wiki/Automatic_differentiation) and is more accurate. Symbolic computation with the package `SymPy.jl` can alse be used.
"""

# ‚ïî‚ïê‚ï° 21495df3-1163-4d7f-afce-ad3600a5411a
varinfo(ForwardDiff.ForwardDiff)

# ‚ïî‚ïê‚ï° 492a1939-ea1e-4912-b8d3-0330624f0ffd
md"""
## Example

Let us find zeros of the function $f_1(x)=e^x-x-\frac{5}{4}$. The form

$$
x=e^x-\frac{5}{4}\equiv \Phi(x)$$

can be used only to compute the negative zero, since in the vicinity of the positive zero $|\varphi'(x)|>1$.
For example, for  $x_0=1$, the sequence diverges very quickly, and for $x_0=0.6$, which is near positive zero, the sequence converges to the negative zero, without theoretical justification. 
"""

# ‚ïî‚ïê‚ï° 7dab9934-414d-44dd-94b5-96a849184687
begin
	œÜ(x)=exp(x)-5.0/4
	plot([f‚ÇÅ,œÜ,x->ForwardDiff.derivative(œÜ,x)],-2.0,2.0,label=["f(x)" "œÜ(x)" "œÜ'(x)"])
end

# ‚ïî‚ïê‚ï° 1e5be7af-8236-4a8c-ab10-4f988350bd6a
Iteration(œÜ,0.5)

# ‚ïî‚ïê‚ï° ed701b40-1ca7-4eba-8ea4-7c2beef3e58d
Iteration(œÜ,1.0)

# ‚ïî‚ïê‚ï° 8eb3ad86-6d79-4016-9f65-5797f51d779e
Iteration(œÜ,0.6)

# ‚ïî‚ïê‚ï° 54214d6b-b630-43d7-afdc-96ebfd4800a1
md"""
The positive zero can be computed using the form

$$
x=\ln\big(x+\frac{5}{4}\big)\equiv \Psi(x).$$
"""

# ‚ïî‚ïê‚ï° 70f9b3bc-ef11-4d7c-af8c-0abc262b92fa
begin
	Œ®(x)=log(x+5.0/4)
	plot([f‚ÇÅ,x->ForwardDiff.derivative(œÜ,x), x->ForwardDiff.derivative(Œ®,x)],
	    -1.0,1.0,label=["f(x)" "œÜ'(x)" "Œ®'(x)"])
end

# ‚ïî‚ïê‚ï° 3ccbe741-478e-4b23-8a64-d6f3380d8b1b
scatter!([Iteration(œÜ,-0.5)[1],Iteration(Œ®,1.0)[1]],[0,0],label="Zeros")

# ‚ïî‚ïê‚ï° 5dee83f3-2171-478a-b811-04c3ccef1a0e
md"""
## Square root of 2

Let us approximate $\sqrt{2}$, that is, find the positive solution of the equation 

$$
x^2-2=0.$$

The equation can be transformed into form (2) as

$$
x=\frac{2}{x},$$

but then $\varphi'(x)=-\displaystyle\frac{2}{x^2}$, so on the interval $[1,2]$ the condition (3) does not hold. Thus, we set

$$
\frac{x}{2}=\frac{1}{x},$$

or

$$
x=\frac{x}{2}+\frac{1}{x}=\frac{1}{2}\left(x+\frac{2}{x}\right)\equiv\varphi(x).$$

Very accurate approximation is attained after just 5 iterations!
"""

# ‚ïî‚ïê‚ï° b27404ef-ac18-4dce-804c-07255ab001c2
begin
	œÜ‚ÇÅ(x)=(x+2.0/x)/2.0
	plot([œÜ‚ÇÅ,x->ForwardDiff.derivative(œÜ‚ÇÅ,x)],1.0,2.0,label=["œÜ‚ÇÅ(x)" "œÜ‚ÇÅ'(x)"])
end

# ‚ïî‚ïê‚ï° 25640f91-4b47-4bcf-b360-499541077c80
Iteration(œÜ‚ÇÅ,1.0,1e-15), ‚àö2

# ‚ïî‚ïê‚ï° 96619f40-3a10-11eb-1529-29d9aa539c03
# Manual computation using rational numbers
begin
	y=1//1
	y‚ÇÅ=(y+2//y)//2
	y‚ÇÇ=(y‚ÇÅ+2//y‚ÇÅ)//2
	y‚ÇÉ=(y‚ÇÇ+2//y‚ÇÇ)//2
	y‚ÇÑ=(y‚ÇÉ+2//y‚ÇÉ)//2
	y‚ÇÖ=(y‚ÇÑ+2//y‚ÇÑ)//2
end

# ‚ïî‚ïê‚ï° bc84a732-3a10-11eb-11ed-1b1abc4f530b
y‚ÇÖ-‚àö2

# ‚ïî‚ïê‚ï° cf6ff8ea-2c1e-442a-bfc6-5eac4f7269dd
begin
	# Let us try ‚àö10
	œÜ‚ÇÇ(x)=(9x+10.0/x)/10.0
	plot([œÜ‚ÇÇ,x->ForwardDiff.derivative(œÜ‚ÇÇ,x)],3.0,4.0,label=["œÜ‚ÇÇ(x)" "œÜ‚ÇÇ'(x)"])
end

# ‚ïî‚ïê‚ï° 4fa06267-97f2-409f-8025-c160362014a9
Iteration(œÜ‚ÇÇ,3.0,1e-10), ‚àö10 # 1e-15

# ‚ïî‚ïê‚ï° 46bebd7f-69ba-402f-91aa-6ad864add60b
begin
	# Try sqrt(10) in a different way
	œÜ‚ÇÉ(x)=(4x+10.0/x)/5.0
	plot([œÜ‚ÇÉ,x->ForwardDiff.derivative(œÜ‚ÇÉ,x)],3.0,4.0)
end

# ‚ïî‚ïê‚ï° 9b237357-f6dc-4054-bee8-1cac4a5aad07
Iteration(œÜ‚ÇÉ,3.0,1e-10), ‚àö10 # 1e-15

# ‚ïî‚ïê‚ï° 4f3b3c3a-0cf4-4bcd-a8ad-9061c7653386
md"""
# Newton's method

__Newton's method__ or __Tangent method__ is based on the following idea: in the vicinity of the starting point $x_0$, the function $f(x)$ is approximated by the tangent line through the point $(x_0,f(x_0))$,

$$
f_1(x)=f(x_0)+f'(x_0)(x-x_0).$$

The next approximation is the intersection of the tangent line and the $x$-axis. In this way we obtain series of approximations:

$$
x_{n+1}=x_n-\frac{f(x_n)}{f'(x_n)},\quad n=0,1,2,\ldots \qquad\qquad (4)$$

We have the following:

__Theorem.__  Let the function $f:[a,b]\to \mathbb{R}$ satisfy:

*  $f''$ is continuous on  $(a,b)$,
*  $f(a)\cdot f(b)<0$,
*  $f'$ and $f''$ have consatnt sign on $(a,b)$, i 
*  for the starting approximation  $x_0\in [a,b]$ it holds $f(x_0)\cdot f''(x_0)>0$.

Then the sequence (4) converges to __unique__ solution $\xi$ of the equation $f(x)=0$. Also, the follwoing  __error bounds__ hold:

$$
\begin{aligned}
|\xi-x_n|&\leq \displaystyle\frac{M_2}{2m_1}(x_n-x_{n-1})^2, \\
|\xi-x_{n+1}|&\leq \displaystyle\frac{M_2}{2m_1}(\xi-x_{n})^2, \\
\end{aligned}$$

where

$$
M_2=\max_{x\in(a,b)}|f''(x)|,\quad
m_1=\min_{x\in(a,b)}|f'(x)|.$$

Therefore, the speed of convergence is __quadratic__.

## Example
"""

# ‚ïî‚ïê‚ï° d8d1e5e8-13c6-43cd-b2ca-aa6e96c142e3
function Newton(f::Function,x::Number,œµ::Float64=1e-10)
    Œæ=x-f(x)/(x->ForwardDiff.derivative(f,x))(x)
    iter=0
    while abs(x-Œæ)>œµ && iter<100
        x=Œæ
        Œæ=x-f(x)/(x->ForwardDiff.derivative(f,x))(x)
        iter+=1
    end
    Œæ,iter
end

# ‚ïî‚ïê‚ï° 6258bcb3-2682-47e5-8db5-0aaf03f32807
begin
	f‚ÇÜ(x)=exp(-x)+x^2-2
	plot(f‚ÇÜ,-3,4,label="f(x)")
end

# ‚ïî‚ïê‚ï° f9376e6e-0595-4045-be73-c59c07e7be34
md"""
Let us check the conditions of the theorem for the positive zero:
"""

# ‚ïî‚ïê‚ï° dca6feb2-e2e3-4c1e-a568-794c610cc611
begin
	a‚ÇÄ=1
	b‚ÇÄ=2
	x‚ÇÄ=1.5
	plot([f‚ÇÜ,x->ForwardDiff.derivative(f‚ÇÜ,x),
	        x->ForwardDiff.derivative(x->ForwardDiff.derivative(f‚ÇÜ,x),x)],a‚ÇÄ,b‚ÇÄ, 
	    label=["f(x)" "f'(x)" "f''(x)"])
end

# ‚ïî‚ïê‚ï° 0cf32332-f169-465a-9315-b9704a31badf
f‚ÇÜ(a‚ÇÄ)*f‚ÇÜ(b‚ÇÄ)<0, 
f‚ÇÜ(x‚ÇÄ)*(x->ForwardDiff.derivative(
        x->ForwardDiff.derivative(f‚ÇÜ,x),x))(x‚ÇÄ)>0

# ‚ïî‚ïê‚ï° 6de6ad42-cad9-40b4-8503-78f1316b3b38
Newton(f‚ÇÜ,x‚ÇÄ) # 1e-15

# ‚ïî‚ïê‚ï° 9ba461d0-3b8c-4ec7-9b14-8ea720a569c3
begin
	# Negative zero
	a=-1
	b=0
	x‚ÇÅ=-1.0
	f‚ÇÜ(a)*f‚ÇÜ(b)<0, 
	f‚ÇÜ(x‚ÇÅ)*(x->ForwardDiff.derivative(
	        x->ForwardDiff.derivative(f‚ÇÜ,x),x))(x‚ÇÅ)>0
end

# ‚ïî‚ïê‚ï° 9609f70c-e5e9-4552-8ecb-8b95cf484d29
Newton(f‚ÇÜ,x‚ÇÅ)

# ‚ïî‚ïê‚ï° 518e1f14-3254-4ca6-9d87-c695008ae79e
begin
	plot(f‚ÇÜ,-1.0,2)
	scatter!([Newton(f‚ÇÜ,x‚ÇÄ)[1],Newton(f‚ÇÜ,x‚ÇÅ)[1]],[0,0])
end

# ‚ïî‚ïê‚ï° b7109008-05de-45db-a383-78b8a6973733
md"""
__Remark.__ If we choose starting values $x_0=1$ and $x_0=0$, respectively, the iterations will also converge towards desired zeros, albeit without theoretical justification: 
"""

# ‚ïî‚ïê‚ï° 0fbd88ae-6c19-41ac-983b-83ba8bd97965
Newton(f‚ÇÜ,1)

# ‚ïî‚ïê‚ï° 917b4fc3-e485-4801-8d16-0e32a22649b5
Newton(f‚ÇÜ,0)

# ‚ïî‚ïê‚ï° 1db0ba3d-e82e-4e4d-8b5d-c1100d04fd2d
md"""
# Secant method

If, in the formula (4), the derivative $f'(x_n)$ is approximated by the finite difference (secant line) though the  __two__ previously obtained points,

$$
f'(x_n)\approx \frac{f(x_n)-f(x_{n-1})}{x_n-x_{n-1}}, $$

we obtain the sequence

$$
x_{n+1}=\frac{x_{n-1}f(x_n)-x_nf(x_{n-1})}{f(x_n)-f(x_{n-1})},\qquad f(x_n)\neq f(x_{n-1}), \quad n=1,2,3,\ldots.$$

Obviously, we need  __two__ initial approximations, $x_0,x_1\in[a,b]$. The convergence properties are similar to those of the Newton's method.
"""

# ‚ïî‚ïê‚ï° dce97964-5846-4642-859e-f041b8a72d76
function Secant(f::Function,x::Number,Œ∂::Number,œµ::Float64=1e-10)
    Œæ=(x*f(Œ∂)-Œ∂*f(x))/(f(Œ∂)-f(x))
    iter=0
    while abs(Œ∂-Œæ)>œµ && iter<100
        x=Œ∂
        Œ∂=Œæ
        Œæ=(x*f(Œ∂)-Œ∂*f(x))/(f(Œ∂)-f(x))
        iter+=1
    end
    Œæ,iter
end

# ‚ïî‚ïê‚ï° 33402e76-139d-4062-b72d-57b45e1c3eec
Secant(f‚ÇÜ,-1,0), Secant(f‚ÇÜ,1,2)

# ‚ïî‚ïê‚ï° Cell order:
# ‚ï†‚ïêd0d29fe8-6542-4968-a695-03371ba85543
# ‚ï†‚ïê3a20c826-03cd-4c06-a0c6-7dc657067feb
# ‚ï†‚ïê7bab2ccd-e6d7-479c-abef-d9ed8eefaf2a
# ‚ïü‚îÄ1198a836-bb14-4b8e-9f30-160097bc4507
# ‚ïü‚îÄ7942e1ac-57f4-493c-9d09-de4154dcd7b9
# ‚ï†‚ïê9d707705-29a2-47fc-87b8-d39ff967efb5
# ‚ïü‚îÄ0233a80f-dfc6-420c-ae39-60828b19010a
# ‚ï†‚ïê0be4ad52-497f-48a4-9f53-b6d1291beb58
# ‚ïü‚îÄ5a17ece9-a499-42c7-8dc8-cc9035f1b311
# ‚ï†‚ïê910d7790-488d-48b8-a951-eee68774f79f
# ‚ï†‚ïê5d233a49-774b-479c-8c80-5de46817614e
# ‚ï†‚ïêaedcd7b9-8ef5-48a8-8ef8-d7dab4b733e0
# ‚ï†‚ïêe7e92425-287a-482b-bb1e-8168b2b130f1
# ‚ï†‚ïê56a6bc1f-7ff8-464d-a864-ec7a19e3f9c3
# ‚ï†‚ïê7097ec9a-b556-4be7-a356-9c9d5003dd73
# ‚ï†‚ïêbdb9d5d9-861c-4fd1-89d5-e0622120f944
# ‚ï†‚ïêf7802590-744f-4175-ba60-5d801fa252ec
# ‚ï†‚ïêd193fd54-c5a7-45f4-a61d-ae532bae4f44
# ‚ï†‚ïê52fbcdb5-ade7-48c0-bc09-0154f3d615b8
# ‚ï†‚ïêe1f25e30-1ff7-4a91-b7cf-393c36c438b6
# ‚ï†‚ïêf33b37c9-2f88-422b-ac9f-40d7945ccb47
# ‚ï†‚ïê4c7dc70c-1832-461c-ad48-5e358b49661f
# ‚ïü‚îÄ69c1d80b-3d24-4ded-a226-391b337805e5
# ‚ï†‚ïêd794399b-9c03-47a1-8a37-40f8545a0d46
# ‚ïü‚îÄ5cc494f2-a789-4455-9ac9-925428d274fe
# ‚ï†‚ïê21495df3-1163-4d7f-afce-ad3600a5411a
# ‚ïü‚îÄ492a1939-ea1e-4912-b8d3-0330624f0ffd
# ‚ï†‚ïê7dab9934-414d-44dd-94b5-96a849184687
# ‚ï†‚ïê1e5be7af-8236-4a8c-ab10-4f988350bd6a
# ‚ï†‚ïêed701b40-1ca7-4eba-8ea4-7c2beef3e58d
# ‚ï†‚ïê8eb3ad86-6d79-4016-9f65-5797f51d779e
# ‚ïü‚îÄ54214d6b-b630-43d7-afdc-96ebfd4800a1
# ‚ï†‚ïê70f9b3bc-ef11-4d7c-af8c-0abc262b92fa
# ‚ï†‚ïê3ccbe741-478e-4b23-8a64-d6f3380d8b1b
# ‚ïü‚îÄ5dee83f3-2171-478a-b811-04c3ccef1a0e
# ‚ï†‚ïêb27404ef-ac18-4dce-804c-07255ab001c2
# ‚ï†‚ïê25640f91-4b47-4bcf-b360-499541077c80
# ‚ï†‚ïê96619f40-3a10-11eb-1529-29d9aa539c03
# ‚ï†‚ïêbc84a732-3a10-11eb-11ed-1b1abc4f530b
# ‚ï†‚ïêcf6ff8ea-2c1e-442a-bfc6-5eac4f7269dd
# ‚ï†‚ïê4fa06267-97f2-409f-8025-c160362014a9
# ‚ï†‚ïê46bebd7f-69ba-402f-91aa-6ad864add60b
# ‚ï†‚ïê9b237357-f6dc-4054-bee8-1cac4a5aad07
# ‚ïü‚îÄ4f3b3c3a-0cf4-4bcd-a8ad-9061c7653386
# ‚ï†‚ïêd8d1e5e8-13c6-43cd-b2ca-aa6e96c142e3
# ‚ï†‚ïê6258bcb3-2682-47e5-8db5-0aaf03f32807
# ‚ïü‚îÄf9376e6e-0595-4045-be73-c59c07e7be34
# ‚ï†‚ïêdca6feb2-e2e3-4c1e-a568-794c610cc611
# ‚ï†‚ïê0cf32332-f169-465a-9315-b9704a31badf
# ‚ï†‚ïê6de6ad42-cad9-40b4-8503-78f1316b3b38
# ‚ï†‚ïê9ba461d0-3b8c-4ec7-9b14-8ea720a569c3
# ‚ï†‚ïê9609f70c-e5e9-4552-8ecb-8b95cf484d29
# ‚ï†‚ïê518e1f14-3254-4ca6-9d87-c695008ae79e
# ‚ïü‚îÄb7109008-05de-45db-a383-78b8a6973733
# ‚ï†‚ïê0fbd88ae-6c19-41ac-983b-83ba8bd97965
# ‚ï†‚ïê917b4fc3-e485-4801-8d16-0e32a22649b5
# ‚ïü‚îÄ1db0ba3d-e82e-4e4d-8b5d-c1100d04fd2d
# ‚ï†‚ïêdce97964-5846-4642-859e-f041b8a72d76
# ‚ï†‚ïê33402e76-139d-4062-b72d-57b45e1c3eec
